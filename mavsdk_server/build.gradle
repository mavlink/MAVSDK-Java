buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'
    }
}

allprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    repositories {
        google()
        mavenCentral()
    }

    def mavsdk_server_release = "v2.14.0"

    task extractMavsdkServer(type: Copy) {
        mkdir project.buildDir.getAbsolutePath() + "/tmp"

        def extractForArch = { arch ->
            def archiveName = "mavsdk_server_android-" + arch + "_" + mavsdk_server_release + ".tar"
            def archiveFile = new File(project.buildDir.getAbsolutePath() + "/tmp/" + archiveName)
            def archiveUrl = "https://github.com/mavlink/MAVSDK/releases/download/" + mavsdk_server_release + "/mavsdk_server_android-" + arch + ".tar"
            def destDir = project.projectDir.getAbsolutePath() + "/src/main/prebuiltLibs"

            if (!archiveFile.exists()) {
                project.logger.lifecycle("Downloading " + archiveFile.getName() + " into " + archiveFile.getAbsolutePath() + "...")
                new URL(archiveUrl).withInputStream { i -> archiveFile.withOutputStream { it << i } }
            } else {
                project.logger.lifecycle("Archive already exists! Skipping download!")
            }

            project.logger.lifecycle("Extracting into " + destDir)
            copy {
                from tarTree(archiveFile)
                eachFile({ file -> if (file.getName().endsWith(".so")) file.setMode(0755) })
                into(destDir)
            }
        }

        extractForArch("arm64")
        extractForArch("arm")
        extractForArch("x86")
        extractForArch("x86_64")
    }
    build.dependsOn extractMavsdkServer

    if (project.hasProperty('ossrhUsername')
            && project.hasProperty('ossrhPassword')) {
        afterEvaluate {
            publishing {
                publications {
                    release(MavenPublication) {
                        from components.release

                        pom {
                            name = 'MAVSDK-Server'
                            packaging = 'aar'
                            description = 'MAVSDK server for Android.'
                            url = 'https://github.com/mavlink/MAVSDK-Java'

                            scm {
                                connection = 'scm:git:https://github.com/mavlink/MAVSDK-Java'
                                developerConnection = 'scm:git:https://github.com/mavlink/MAVSDK-Java'
                                url = 'https://github.com/mavlink/MAVSDK-Java'
                            }

                            licenses {
                                license {
                                    name = 'BSD 3-Clause "New"'
                                    url = 'https://opensource.org/licenses/BSD-3-Clause'
                                }
                            }

                            developers {
                                developer {
                                    id = 'jonasvautherin'
                                    name = 'Jonas Vautherin'
                                    email = 'dev@jonas.vautherin.ch'
                                }
                            }
                        }
                    }
                }
                repositories {
                    maven {
                        url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

                        credentials {
                            username = ossrhUsername
                            password = ossrhPassword
                        }
                    }
                }
            }

            signing {
                useGpgCmd()
                sign publishing.publications.release
            }
        }
    }
}

android {
    compileSdkVersion 33

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33

        archivesBaseName = 'mavsdk-server'
        group = 'io.mavsdk'
        versionCode 3
        version '2.1.5'

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
        }

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }

    ndkVersion "25.1.8937393"
    namespace 'io.mavsdk.mavsdkserver'
}
