buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.18"
    }
}

subprojects {
    apply plugin: 'com.google.protobuf'
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'signing'

    group = 'io.mavsdk'
    archivesBaseName = 'mavsdk'
    version = '0.12.0'

    repositories {
        jcenter()
    }

    configurations {
        checkstyleClasspath
    }

    dependencies {
        checkstyleClasspath 'com.puppycrawl.tools:checkstyle:8.17'
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    def grpcVersion = '1.42.1'

    protobuf {
        protoc {
            artifact = 'com.google.protobuf:protoc:3.17.2' // Same protoc version as grpc-java
        }

        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
            }
        }

        generateProtoTasks {
            all()*.each { task ->
                task.builtins {
                    java {
                        option "lite"
                    }
                }

                task.plugins {
                    grpc {
                        option 'lite'
                    }

                    mavsdk {
                        option 'file_ext=java'
                        option "template_path=$project.rootDir/templates/"
                    }
                }
            }
        }
    }

    sourceSets {
        main {
            proto {
                srcDir "$project.rootDir/proto/protos/"
            }

            java {
                srcDir 'build/generated/source/proto/main/javalite'
                srcDir 'build/generated/source/proto/main/grpc'
                srcDir 'build/generated/source/proto/main/mavsdk'
            }
        }
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    if (project.hasProperty('ossrhUsername')
        && project.hasProperty('ossrhPassword')) {

    signing {
        useGpgCmd()
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'MAVSDK-Java'
                    packaging 'jar'
                    description 'MAVSDK client for Java.'
                    url 'https://github.com/mavlink/MAVSDK-Java'

                    scm {
                        connection 'scm:git:https://github.com/mavlink/MAVSDK-Java'
                        developerConnection 'scm:git:https://github.com/mavlink/MAVSDK-Java'
                        url 'https://github.com/mavlink/MAVSDK-Java'
                    }

                    licenses {
                        license {
                            name 'BSD 3-Clause "New"'
                            url 'https://opensource.org/licenses/BSD-3-Clause'
                        }
                    }

                    developers {
                        developer {
                            id 'jonasvautherin'
                            name 'Jonas Vautherin'
                            email 'jonas.vautherin@gmail.com'
                        }
                    }
                }
            }
        }
    }
    }

    task checkstyle(type: Checkstyle) {
        configFile = rootProject.file("config/checkstyle/checkstyle.xml")
        configDir = project.provider({ rootProject.file("config/checkstyle") })
        source 'src'
        include '**/*.java'
        exclude '**/generated/**'

        classpath = project.files()
        checkstyleClasspath = project.configurations.checkstyleClasspath

        maxWarnings 0

        reports {
            xml.enabled true
            xml.destination new File("$project.buildDir/reports/checkstyle/checkstyle_report.xml")

            html.enabled true
            html.destination new File("$project.buildDir/reports/checkstyle/checkstyle_report.html")
        }
    }

    build.dependsOn 'checkstyle'

    idea {
        module {
            inheritOutputDirs = false
            outputDir = compileJava.destinationDir
            testOutputDir = compileTestJava.destinationDir
        }
    }

    dependencies {
        // The protobuf configuration which was set earlier would generate well-known
        // protos which would create conflicts with other libraries that use them as well
        compileOnly "io.grpc:grpc-protobuf:${grpcVersion}"

        implementation "io.grpc:grpc-okhttp:${grpcVersion}"
        implementation "io.grpc:grpc-protobuf-lite:${grpcVersion}"
        implementation "io.grpc:grpc-stub:${grpcVersion}"
        implementation 'org.slf4j:slf4j-api:1.7.25'
        api 'io.reactivex.rxjava2:rxjava:2.2.21'

        compileOnly "javax.annotation:javax.annotation-api:1.3.2"

        testImplementation "io.grpc:grpc-testing:${grpcVersion}"
        testImplementation 'junit:junit:4.12'
        testImplementation 'org.mockito:mockito-core:2.13.0'
    }
}
